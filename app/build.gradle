apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    signingConfigs {
// release keystore not ready yet
//        release {
//            keyAlias 'release'
//            keyPassword 'keystore-password'
//            storeFile file('../../travelers-diary_release.keystore')
//            storePassword 'keystore-password'
//        }
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('../../travelers-diary_debug.keystore')
            storePassword 'android'
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.travelersdiary"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        // All keys defined in gradle.properties file
        // Replace all keys with your keys
        it.buildConfigField 'String', 'GOOGLE_API_KEY', GoogleApiKey
        it.buildConfigField 'String', 'GOOGLE_API_SERVER_KEY', GoogleApiServerKey
        it.buildConfigField 'String', 'GOOGLE_ANDROID_CLIENT_ID', GoogleAndroidClientId
        it.buildConfigField 'String', 'FIREBASE_ROOT_URL', FirebaseRootUrl
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    task indexAssets {
        description 'Index Build Variant assets for faster lookup by AssetManager'

        ext.assetsSrcDir = file( "${projectDir}/src/main/assets" )

        inputs.dir assetsSrcDir

        doLast {
            android.applicationVariants.each { target ->
                // create index
                def contents = ""
                def tree = fileTree(dir: "${ext.assetsSrcDir}", include: ['**/*.ttf'], exclude: ['**/.svn/**', '*.index'])
                // use this instead if you have assets folders in each flavor:
                // def tree = fileTree(dir: "${ext.variantPath}", exclude: ['**/.svn/**', '*.index'])
                tree.visit { fileDetails ->
                    contents += "${fileDetails.relativePath}" + "\n"
                }

                // create index file
                def assetIndexFile = new File("${ext.assetsSrcDir}/assets.index")
                assetIndexFile.write contents
            }
        }
    }

    indexAssets.dependsOn {
        tasks.matching { task -> task.name.startsWith( 'merge' ) && task.name.endsWith( 'Assets' ) }
    }

    tasks.withType( JavaCompile ) {
        compileTask -> compileTask.dependsOn indexAssets
    }

    indexAssets
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    /*Support libs*/
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'

    /*Google services*/
    compile 'com.google.android.gms:play-services-auth:8.4.0'
    compile 'com.google.android.gms:play-services-plus:8.4.0'
    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile 'com.google.android.gms:play-services-location:8.4.0'

    /*Serialization*/
    compile 'com.google.code.gson:gson:2.4'

    /*Custom views*/
    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile 'com.1gravity:android-rteditor:1.4.0'
    compile 'com.github.darsh2:MultipleImageSelect:v0.0.3'
    compile 'com.sangcomz:FishBun:0.3.0@aar'

    /*Firebase*/
    compile 'com.firebase:firebase-client-android:2.5.0+'
    compile 'com.firebaseui:firebase-ui:0.3.0'
    compile 'com.firebase:geofire:1.1.+'

    /*Others*/
    compile 'com.jakewharton:butterknife:7.0.1'

    compile 'com.squareup:otto:1.3.8'
    apt 'com.squareup:otto:1.3.8'

    compile 'com.squareup.okhttp3:okhttp:3.0.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
}

apply plugin: 'com.google.gms.google-services'